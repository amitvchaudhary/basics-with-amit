---
title: Processes
description: Let the process crash
createdAt: 2023-03-08
head.title: Processes
head.description: Let the process crash
---

::post-image{imageSrc="header_5.jpg"}
Processes
::

If you have just started learning Elixir, then you must have read about LiveView components. They are super powerful and I believe they will replace React/Angular/VueJS one day. I claim this because I have used all aforementioned frameworks for quite a long time and I strongly feel, Elixir LiveView will be the future for frontend development. But before you accept what I just said and start creating new project, I suggest you to understand this post thoroughly. This doesn't mean you can't start with LiveView without reading this post, but if you know what goes under the hood, you will appreciate its architecture and design decisions. In this post we are NOT going to talk about LiveView, but what powers it - **Process**.

**Tip:** :moneybag: If you want to convince your developer or manager to start using Elixir in projects, you won't succeed if you can't explain why. This post will help you in answering 'why'. :sunflower:

Every business person wants his product (system) to run for 24x7 and make his customers happy. But this wish can only be fulfilled if the system fulfills below parameters:

- _Fault-tolerance_: Recover from the effects of runtime errors.
- _Scalability_: Handle increase in requests by adding more hardware resources, but not redeploying the code.
- _Distribution_: If one server fails, another takes over the work

You might have read above terms with respect to **Cloud Computing**. It's because in cloud, the cloud provider takes care of them. But it doesn't come for free. You need to pay. Sometimes, even after spending good amount of money, business owners or backend developers can't do much due to poor choice of backend language. But what if I tell you, from my own experience, that you can reduce your billing if you use Elixir? It sounds lucrative and it is. The reason for that is again a **Process**.

In this post, when I say _Process_, I mean BEAM Process and NOT an OS process. BEAM is a Virtual Machine that runs the processes. The BEAM process is way lighter than OS process. In fact whole BEAM runs on a single OS process. You must be knowing that OS process can spawn multiple **threads** to achieve concurrency. So, it means a thread is lighter than an OS process. But BEAM process is so light that it takes couple of microseconds to create and just few KBs as compared to OS thread which takes couple of megabytes even just for the stack. It is said that BEAM VM can spawn roughly 134 million processes :open_mouth: FYI, ther is no concept of **threads** in Elixir.

![](/images/5_1.png){m-auto}

You can see in the above diagram that how one OS process has four OS threads running BEAM schedulers. The no. of schedulers depend on the no. of OS threads. If you have quad-core machine, then you can have four OS threads which can handle four BEAM schedulers. A scheduler is in charge of the interchangeable execution of processes. Each process gets some time; after the time is up, the running process is preempted, and the next one takes over.

## Anatomy

Process is an isolated entity where code execution happens. It's just a block of memory where you are going to store data and manipulate it. The best part of process is that it doesn't share its state with another process, unlike threads (Java, etc). Because of this, you can run tasks simultaneously without worring about dependency. If one process crashes, it won't affect the other ones. But, if you want that behavior, you can. We will see that little later. But when we have multiple processes running parallelly, we can achieve _scalability_ to address a load increase by adding more hardware power. BEAM scheduler will ensure there is proper utilization of CPU resources. Because process runs in isolation, it also guarantees _fault tolerance_ as it localizes the impact of runtime errors.

![](/images/5_2.png){m-auto}

You can see in the above diagram that process is just a block of memory with following parts:

- **Stack** - To keep local variables
- **Heap** - To keep larger structures
- **Mailbox** - To store messages sent from other processes
- **PCB** - A process control block to keep track of the current state
- **PID** - A process identifier to uniquely identify the process

We will focus more on **mailbox** and **pid** as rest are self-explanatory to any developer.

### One Process

Before you learn to create a process, you might not know that till now, if you are using `iex` terminal to practice Elixir, you have been using process. Yes, the shell is a process. The first thing to know about any process is its identifier, _pid_. To get the _pid_ of your shell, just run `self()` function.

```elixir
iex> self()
#PID<0.107.0>
```

`#PID<0.107.0>` is the pointer to a process. It's its identity. You can also give a name to it, but we will see that later. The three integers `0.107.0` are unique in the current run of Elixir. If you close the terminal and open again and print the pid again, you might get a different pid this time. Can you assign pid manually to any process? No. It's read-only.

```elixir
iex> #PID<0.107.0>
nil
```

You can know pid of any process as long as you can reach there. If you have process running on another computer and if you are in same cluster, you can reach that process and thus get its pid.

Without knowing pids, you can't communicate with other processes. Pids act like addresses for mailboxes, where messages are dropped from other processes. But yes, a process can also drop message to itself. To send a message, to self or any other process, you need to use `send/2` function. The first argument is the pid and second one is the message. The message can be of any type - atom, map, list, etc.

```elixir
iex> send(self(), :hello)
:hello
iex> pid = self()
#PID<0.107.0>
iex> send(pid, :amit)
:amit
iex> send(pid, :amit)
:amit
```

![](/images/5_3.png){m-auto}

Here, the process is sending the messages to itself. Interesting thing to note here is that the output `:hello` and `:amit` is from the function `send/2` and not from the its mailbox. The messages are still lying in the mailbox. If you want to see the messages in the mailbox, use `flush()` function. But one thing to be aware is that it will remove those messages as well.

```elixir
iex> flush()
:hello
:amit
:amit
:ok
iex> flush()
:ok
```

You must observe one more thing in above output. Though we had sent same message `:amit` twice, it created another message and didn't replace the previous one. Each message is isolated. Also `:ok` is the result of `flush()` function notifying that it was successful in fetching the messages from the mailbox.

`flush()` can be useful in many scenarios. It empties the mailbox everytime you use it. But it would be good if we can process individual message and act accordingly. For that we can use `receive...end` construct.

```elixir
iex> send(self(), :amit)
:amit
iex> receive do
...>   x -> x
...> end
:amit
iex> send(self(), 20)
20
iex> receive do
...>   x -> x * 2
...> end
40
```

`receive...end` construct waits for a message and exits after fetching one. So, if there is no message in the mailbox or you have some other pattern in the construct which doesn't meet the message format, the process will hang there. Try below:

- **Case 1:** no messages

```elixir
iex> send(self(), :amit)
:amit # printed by send() function
iex> receive do
...>   x -> x
...> end
:amit # received from mailbox, so no more messages now
iex> receive do
...>   x -> x
...> end
# in hang state. Press Ctrl + C to exit and restart iex
```

- **Case 2:** unexpected pattern

```elixir
iex> send(self(), :amit)
:amit # printed by send() function
iex> receive do
...>   :hello -> :hello # expecting :hello but received :amit
...> end
# in hang state. Press Ctrl + C to exit and restart iex
```

**Case 1** is self-explanatory but why did it stuck in **Case 2**? Because it thinks that some process will send some message with `:hello` format. If it does, then it will print it and exit.

To avoid keeping process in hung state, you can use `after` construct of the `receive ... end` clause.

```elixir
iex> receive do
...>    :hello -> :hello # expecting :hello but received :amit
...> after
...>   30000 -> IO.puts("Stopping, no more messages")
...>  end
Stopping, no more messages
:ok
```

Intersting thing to know is that it didn't raise an error when there was no pattern match unlike explained in below topic.

::related-posts
[- Error Handling](error-handling)
::

Because, the `receive` construct is an exception to this rule. If a message doesn't match any of the provided clauses, it's put back into the mailbox and the next message is processed. This is how the flow looks like:

1. Take the first message from mailbox
2. Match the pattern from top to bottom
3. If there is a match, run the corresponding code
4. If there is no match, put the message back to same position in mailbox and try next message
5. If there is no more message in the mailbox, wait for it and one there is any, start from picking the first message in the mailbox
6. If you have `after` clause mentioned, then it will run if there are no more messages in the mailbox within the specified time

### Two Processes

Till now we played with the process that was already there, shell process. Now, it's time to send message from shell process to another. But for that we need to create another process first. For that you can use `spawn/1` function. This function takes zero-arity lambda that will run in another process and return process indentifier. Once the other process completes its work, it exits. While the other process does its work, the caller process won't be blocked, it can continue its work as both processes are running in parallel.

```elixir
iex> spawn(fn -> IO.puts("I am a new process") end)
I am a new process
#PID<0.109.0>
```

Here, you got a pid `#PID<0.109.0>` of another process and also the output. We gave the process an anonymous function to execute and it printed `I am a new process`. But there is one more thing.

```elixir
iex> spawn(fn -> 1 + 1 end)
#PID<0.111.0>
```

Why didn't you get `2` as an output? Because we didn't tell the process to send the computed result back to us (shell process). We just told to do computation. Then how come the `I am a new process` got printed in previous process? That's because you told to print the result in ANOTHER process. That was it's main job. It didn't send anything back. If you want the result back, then you need to send your (shell process) pid `#PID<0.107.0>` to another process `#PID<0.111.0>` and tell it to send the result back with the help of `send/2` as we had seen earlier.

Now how do you check whether the newly created process is still alive (running or not)?

```elixir
iex> pid = spawn(fn ->
...> Process.sleep(10000)
...> IO.puts('I slept for 10 seconds')
...> end
...> )
#PID<0.120.0>
iex> Process.alive?(pid)
true
I slept for 10 seconds
iex> Process.alive?(pid)
false
```

Because `Process.alive?(pid)` was executed just before 10 seconds completed, it printed `true`. Now try below code.

```elixir
iex> Process.alive?(self())
true
```

I am sure you know why it printed `true`. Let's move ahead.

It's more common to use `spawn/1` and pass zero-arity lambdas for testing purpose. But in real code, you will most like use modules and named functions. Processes at their heart are just functions and by now you know how to define functions in modules. If not, please go through below post first.

::related-posts
[- Functions](functions)
::

Let's define a module called `Robot` and define a named called function `show` whose main responsibility to just display messages.

```elixir
defmodule Robot do
  def show do
    receive do
      msg -> IO.puts("Received #{msg}")
    end
  end
end
```

We are well aware about `receive ... end` construct. It's is quite similar to `case ... end` construct. So, we just put `receive ... end` inside `show/0` function so that we don't need to write `receive ... end` everytime we want to fetch one message and just call `show/0`. This time, to create a process, we will use `spawn/3` function instead.

```elixir
iex> pid = spawn(Robot, :show, [])
#PID<0.138.0>
```

The arguments are the module name, function name (as an atom), and a list of arguments to the function. You must have noticed that though `show/0` function doesn't accept any parameter, it is still necessary to pass an empty list `[]`.

Now, it's time to send our first message.

```elixir
iex> send(pid, "Wow ...")
Received Wow ...
"Wow ..."
```

`Received Wow ... ` is from the spawned process whereas `Wow ...` is from `send/2` function. Now let's send one more message.

```elixir
iex> send(pid, "Second message ...")
"Second message ..."
```

You see that output is from `send/2` function and not the other process. This is because `show` process exited after fetching the first message `Wow ...`. This also means that if you send a message to a stopped process, you won't get an error but you will also not get any notification that the message was received because no process is listening to it.

We need a process which runs for long time (or forever). We can give such process a name, _server process_. Is there a special way to create a server process? Actually no. We use the same `spawn()` function but use recursion technique in the process to keep it running.

```elixir
defmodule Robot do
  def show do
    receive do
      msg -> IO.puts("Received #{msg}")
      show() # recursion
    end
  end
end
```

We are just telling the process to call `show()` function again after it finishes to process the first message. This will make it wait until the new message arrives and it will go on like that.

```elixir
iex> send(pid, "first")
Received first
"first"
iex> send(pid, "second")
"second"
Received second
iex> send(pid, "third")
Received third
"third"
```

::bonus{noOfStars = 5}
If you notice the second message carefully, the message `Received second` was printed after the message from `send()`. This is due to the fact that processes are asynchronous, so the order may differ every time you send a message.
::

We can improve our process a little bit by introducing accumulator to keep track of no. of messages the process prints.

```elixir
defmodule Robot do
  def show(count) do
    receive do
      msg -> IO.puts("Received #{count}: #{msg}")
      show(count + 1)
    end
  end
end
```

Before you spawn a new process, ensure you pass initial value in the argument list.

```elixir
iex> pid = spawn(Robot, :show, [1])
#PID<0.162.0>
```

Now, let's send the messages again.

```elixir
iex> send(pid, "first")
Received 1: first
"first"
iex> send(pid, "second")
Received 2: second
"second"
iex> send(pid, "third")
Received 3: third
"third"
```

The output looks satisfactory. But won't recursion cause stack overflow? Answer is **No**. But why? Unlike other programming languages, in Elixir, if the last thing that a function does is call another function (or itself), then it is not pushed to stack or allocated memory, but it jumps (similar to go-to statement). This is called tail call optimization. This way, you can have endless loop without being worried about memory leakage.

You can also rewrite above code using return value of `receive` construct this way. This is because each Elixir expression returns a value, and `receive` is no different. The result of `receive` is the result of the last expression in the appropriate clause.

```elixir
defmodule Robot do
  def show(count) do
   new_count = receive do
      msg -> IO.puts("Received #{count}: #{msg}")
      count + 1
    end
    show(new_count)
  end
end
```

This structure helps you think the return value of the `receive` block as a **state** to be preserved between function calls. Here we are using counter, but it can be anything - map, list, etc.

## Naming the Process

Every process has a `pid` and keeping the pids in the form of `pid1`, `pid2`, `pid3`, etc is not an ideal way when dealing with multiple processes. It's fine if you have short running process, but if you have long running processes (server process), it's better to communicate using names. It's similar to IP address and Domain name problem. In Elixir (in other words BEAM), you can register a process using `Process.register/2` built-in function. The first argument is pid and second one is an atom. It accepts atom as name because it guaranteses that every process has unique name, which is the default nature of atoms in Elixir.

```elixir
iex> pid1 = spawn(Robot, :show, [1])
#PID<0.180.0>
iex> Process.register(pid1, :robot)
true
iex> send(:robot, "Hi Robot...")
"Hi Robot..."
Received 1: Hi Robot...
iex> send(:robot, "How are you?")
Received 2: How are you?
"How are you?"
iex>
```

If you try to call a process with a name that doesn't exist (or even that has crashed for some reason), you will get a bad arguments error, which is quite obvious.

```elixir
iex> send(:amit, "How are you?")
** (ArgumentError) errors were found at the given arguments:

  * 1st argument: invalid destination

    :erlang.send(:amit, "How are you?")
    iex:25: (file)
```

::bonus{noOfStars = 2}
You can reuse the name which you provided to other process, if that process is no more alive.
::

If there is a need to know the pid of the process through name, you can use `Process.whereis/1`.

```elixir
iex> Process.whereis(:robot)
#PID<0.180.0>
```

If the process doesn't exist, you will get `nil`.

Now, if there is a need to unregister the process, you can use `unregister/1` function. It won't kill the process, but just deallocates the name.

Also, if you want to list all the processes, just use `Process.registered/0`. You see the `:robot` process?

```elixir
iex> Process.registered
[:global_name_server, :kernel_safe_sup, :kernel_sup, :inet_db, IEx.Broker,
 :socket_registry, :logger_handler_watcher, :standard_error, :file_server_2,
 :user_drv, :robot, :elixir_code_server, :erts_code_purger, :erl_prim_loader,
 :init, :code_server, :erl_signal_server, IEx.Supervisor, :standard_error_sup,
 :global_group, :application_controller, IEx.Pry, Logger.Supervisor,
 :logger_sup, :global_group_check, :elixir_sup, :kernel_refc, :rex,
 :elixir_config, IEx.Config, :user, :logger, Logger, Logger.BackendSupervisor,
 :logger_proxy]
```

You may think that all these processes are global. And it's bad if you look at it from other language's persepctive in terms of _global variables_. But it's not what you think. These processes are per BEAM instance. So, they are **local** to the BEAM instance. You can have multiple BEAM instances connected in distributed systems running with same names, but they are unique.

## Let It Crash

This is a common statement in Elixir docs and community. Process is meant to process something, but when it can't, it fails and crashes.

```elixir
defmodule Robot do
  def show do
   receive do
      x -> IO.puts("#{x} / 2 = #{x / 2}")
      show()
    end
  end
end
```

As long as you pass numbers, it's fine but try to send any other value. It will immediately crash and die. RIP.

```elixir
iex> pid = spawn(Robot, :show, [])
#PID<0.125.0>
iex> send(pid, 4)
4
4 / 2 = 2.0
iex> send(pid, 0)
0 / 2 = 0.0
0
iex> send(pid, :not_a_number)
:not_a_number
iex>
17:09:51.627 [error] Process #PID<0.125.0> raised an exception
** (ArithmeticError) bad argument in arithmetic expression
    iex:4: Robot.show/0
```

This is quite normal in Elixir. If a process dies in between, it's supervisor can replace it with new one, if required. The good part is that dying process doesn't corrupt any other process's state. And whatever state it maintains will be wiped out after it.

## Send and Receive

Till now we saw how individual process behaves. Now, it's time to see process-to-process communication. One would be our shell process which will send messages and another process will receive those messages and do some computation and send the result back to the shell process. So, let's define a module called Calculator and have one public function `display` which will be converted into a standalone process. This process will call its private functions and send the result back to the calling process - shell.

```elixir
defmodule Calculator do
  def display do
    receive do
      {from, operation, num1, num2} ->
        send(from, {operation, calculate(operation, num1, num2)})
        display()
    end
  end

  defp calculate(:sum, num1, num2) do
    num1 + num2
  end

  defp calculate(:multiply, num1, num2) do
    num1 * num2
  end
end
```

Above code looks quite simple as you can see the process sends the result back using same `send/2` function as we had seen before. The important thing is that it must receive the pid which we are capturing into `from` variable. Sending a message without including the sender's pid is like calling a friend and telling him to call you back without giving your number (imagine you can't see the number).

```elixir
pid = spawn(Calculator, :display, [])
#PID<0.157.0>
iex> send(pid, {self(), :sum, 1, 2})
{#PID<0.107.0>, :sum, 1, 2}
iex> send(pid, {self(), :multiply, 2, 2})
{#PID<0.107.0>, :multiply, 2, 2}
```

But where are the results? Of course, they are in the calling process's mailbox. So you can use either `receive` construct or `flush` function to see all message at once.

```elixir
iex> flush
{:sum, 3}
{:multiply, 4}
:ok
```

As we have not added `after` clause in the process, it will keep waiting for new messages. So try from your end to send few more messages. How do I know if it's waiting? You can use `Process.info/0` to get all the details of the running process and check `status` field.

```elixir
iex> Process.info(pid)
[
  current_function: {Calculator, :display, 0},
  initial_call: {Calculator, :display, 0},
  status: :waiting,
  message_queue_len: 0,
  links: [],
  dictionary: [],
  trap_exit: false,
  error_handler: :error_handler,
  priority: :normal,
  group_leader: #PID<0.66.0>,
  total_heap_size: 233,
  heap_size: 233,
  stack_size: 3,
  reductions: 41,
  garbage_collection: [
    max_heap_size: %{error_logger: true, kill: true, size: 0},
    min_bin_vheap_size: 46422,
    min_heap_size: 233,
    fullsweep_after: 65535,
    minor_gcs: 0
  ],
  suspending: []
]
```

As we already have the above information, let dig little further into it. If you just want to see specific fields of interest, you can use `Process.info/2` where you can send the list of fields as second argument in form of atoms.

```elixir
iex> Process.info(pid, [:heap_size, :stack_size])
[heap_size: 233, stack_size: 3]
```

Some interesting fields worth knowing are:

- `message_queue_len`: no. of messages waiting in mailbox
- `dictionary`: it stores the history. Try running few commands and check this field again. You can
- `reductions`: very important one when it comes to debugging. A reduction is one function call within a process. So, in above output, we have reductions as `41`. That indicates there were `41` function calls. The higher the number, higher the CPU consumption. But if there is a long running process, then obviously this number would be high. So, treat this number as a **unit of work** and not just a function call. This is quite helpful when you want to debug which process is consuming most of the CPU. If you are interested in finding, then below code snippet can help you.

```elixir
Process.list() # gets a list of all running pids
|> Enum.map(fn pid ->
  info = Process.info(pid, [:reductions, :registered_name])

  # not every process has a name, if it doesn't, fallback to the pid
  name = case info[:registered_name] do
    [] -> pid
    name -> name
  end

  %{name: name, reductions: info[:reductions]}
end)
|> Enum.sort_by(fn p -> p[:reductions] end, :desc)
|> Enum.take(10)
```

Top 10 processes on my machine:

```elixir
[
  %{name: :erl_prim_loader, reductions: 241106},
  %{name: :code_server, reductions: 154893},
  %{name: #PID<0.107.0>, reductions: 123925},
  %{name: :application_controller, reductions: 71721},
  %{name: :user_drv, reductions: 34582},
  %{name: #PID<0.66.0>, reductions: 17477},
  %{name: :init, reductions: 7189},
  %{name: :kernel_sup, reductions: 3155},
  %{name: Logger, reductions: 1610},
  %{name: #PID<0.80.0>, reductions: 1139}
]
```

::bonus{noOfStars = 2}
From Erlang/OTP 19.2 and later, a context switch is forced when the number reaches 4000 in a process.
::

## Processes and Modules
