---
title: Patterns, Patterns and Patterns
description: Destructure Tuples, Maps, and Lists with Patterns
createdAt: 2022-12-23
head.title: Patterns, Patterns and Patterns
head.description: Many facets of patterns
---

::post-image{imageSrc="header_2.jpg"}
Patterns, Patterns and Patterns
::

If there is one thing you definitely would like to master in Elixir, it is **Pattern Matching**. It helps in keeping your code _concise_ and _readable_. People who come from JavaScript world might be familiar with a concept called _object destructuring_. I wouldn't argue it is similar to pattern matching in Elixir, but pattern matching concept is way more than just looking at it as destructred objects. Let's start with the most confusing operator (=) in Elixir.

## The Match Operator

The most visible operator in Elixir is (=) **match operator**. Why is it called _match operator_ and not an _assignment operator_?

```elixir
iex> 5 = 4 + 1
5
```

It might be a bit strange to see above expression in code as the left side is not a variable but a constant. Still it evaluates and prints the result. The reason is that we are **not** assigning a value, but **matching** both sides. This proves that `=` is not an assignment operator. What if you try below?

```elixir
iex> 5 = 4 + 2
** (MatchError) no match of right hand side value: 6
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:4: (file)
```

It failed because both sides are **not** equal. By this, I am not trying to say that we cannot assign a value to a variable using _match operator_, but the left side **has** to be a variable for assignment operation to succeed. Here, as it was `5`, it was expecting the right side `4 + 2` expression to evaluate to `5` constant. If it were variable, it would have succeeded the match.

```elixir
iex> no_of_balls = 12
12
iex> no_of_balls = 14
14
iex> no_of_balls
14
```

This is how we create a variable. As we know, Elixir is a dynamic programming language, we don't explicitly need to declare variable or its type. The type of a variable is determined by whatever data it stores at the moment.

At runtime, the left side of the `=` operator is matched to the right side. The left side is called a _pattern_, whereas right side is called an _expression_ which evaluates to an Elixir term. The variable `no_of_balls` is matched with the right-side term `12`. A variable **always** matches the right-side term, and it is **bound** to the value of that term.

::bonus{noOfStars = 5}
You must have read that data in Elixir is immutable, then how come we are able to reassign new value to _no_of_balls_ variable? Well it's true. Once a memory location is occupied by some variable, it can't be modified until it's released. What you see in above code is **rebinding** and NOT **reassigning**. It means, the variable now points to a different memory location. In other words, variables are mutable, but the data they point to is immutable.

You don't need to worry about memory management as Elixir is a garbage-collected language, which means when a vairable is out of scope, the corresponding memory is eligible for garbage collection and will be released once the garbage collector starts cleaning.
::

What if you don't want rebinding to happen? Well you can inform Elixir for the same using `^` (pin) operator.

## The Pin Operator

You can lock a variable using `^` (pin) operator.

```elixir
iex> no_of_balls = 12
12
iex> ^no_of_balls = 14
** (MatchError) no match of right hand side value: 14
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:8: (file)
```

It is failing because now the left side is not just a variable, but a **pinned variable**. So, at runtime, it is trying to compare `12 = 14` that's why it is throwing above error. We can take advantage of pin operator in pattern matching in lists, tuples, etc which we will talk about in sometime.

## Tuples

A tuple helps us to store multiple values. It's one of the widely used data structures in Elixir.

```elixir
iex> name = {"amit", "chaudhary"}
{"amit", "chaudhary"}
```

We have bound the tuple `{"amit", "chaudhary"}` with `name` variable in above snippet. If we want to access individual values, we can use pattern matching.

```elixir
iex> {firstName, lastName} = {"amit", "chaudhary"}
{"amit", "chaudhary"}
iex> firstName
"amit"
iex> lastName
"chaudhary"
```

First, it compares that both sides are tuples and have same no. of elements. If the check succeeds, the variables **firstName** and **lastName** are bound to the corresponding elements of tuple. This is extremely useful when you are playing with function parameters and return values.

Let's consider below example. Here `:calendar.local_time()` returns tuple and we can capture `date` and `time` values in variables which can be decomposed further.

```elixir
iex> {date, time} = :calendar.local_time()
{{2022, 12, 23}, {17, 44, 42}}

iex> {year, month, day} = date
{2022, 12, 20}
iex> year
2022
iex> month
12
iex> day
23

iex> {hour, minute, second} = time
{17, 44, 42}
iex> hour
17
iex> minute
44
iex> second
42
```

As discussed, it is important that you know beforehand how many elements would be returned in a tuple by any function.

```elixir
iex> {year, month, day, hour} = date
** (MatchError) no match of right hand side value: {2022, 12, 20}
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:13: (file)
```

In above example, `hour` doesn't exist in the **date** tuple, thus it failed. But what if we want to ignore some elements on the left side?

```elixir
iex> {year, month} = date
** (MatchError) no match of right hand side value: {2022, 12, 21}
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:2: (file)
```

Here, even though `year` and `month` elements exist in **date** tuple, it still failed because the no. of elements didn't match. Because, this is not destructuring in JavaScript world. To avoid this, we can use **anonymous variables** to make the count same and ignore unwanted variables. Anonymous variables are defined using `_` (underscore).

```elixir
iex> {year, month, _} = date
{2022, 12, 21}
```

OR

```elixir
iex> {year, month, _day} = date
{2022, 12, 21}
```

Unlike normal variables, _anonymous variables_ can't be bound. Technically speaking, you could use that variable in the rest of the program, but the compiler will spit out a warning.

```elixir
iex> _day
warning: the underscored variable "_day" is used after being set. A leading underscore indicates that the value of the variable should be ignored. If this is intended please rename the variable to remove the underscore
  iex:4

21
```

How about just extracting `day` from the function? Well we can use nested patterns as well.

```elixir
iex>  {{_, _, day}, _} = :calendar.local_time()
{{2022, 12, 21}, {17, 45, 14}}
iex> day
21
```

Below code will pass because on both sides we have `:ok` atoms and the value `15` will be bound to `result` variable. It shows that is not necessary to have only variables on the left side.

```elixir
iex> {:ok, result} = {:ok, 15}
{:ok, 15}
```

But below code won't compile as atoms are different even though no. of elements are same. Actually, due to this very same nature of pattern matching helps us to write concise code. We will see how in further sections.

```elixir
iex> {:ok, result} = {:error, 15}
** (MatchError) no match of right hand side value: {:error, 15}
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:14: (file)
```

## Lists

List matching works similarly to tuples but with one advantage. Let's say we have a list of people going to attend birthday party.

```elixir
iex> people = [
...> %{age: "30", gender: "Male", name: "Amit"},
...> %{age: "30", gender: "Male", name: "Darshit"},
...> %{age: "35", gender: "Female", name: "Dharini"},
...> %{age: "32", gender: "Male", name: "Kushal"},
...> %{age: "32", gender: "Male", name: "Dishank"},
...> %{age: "40", gender: "Female", name: "Nisha"},
...> %{age: "41", gender: "Male", name: "Naresh"},
...> %{age: "45", gender: "Male", name: "Jagdish"}
...> ]
```

From the above list, we can fetch first 3 people and ignore the remaining ones.

```elixir
iex> [first, second, third | others] = people
iex> first
%{age: "30", gender: "Male", name: "Amit"}
iex> second
%{age: "30", gender: "Male", name: "Darshit"}
iex> third
%{age: "35", gender: "Female", name: "Dharini"}
iex>
```

You can again extract another 3 people from `others` list.

```elixir
iex> [first, second, third | remainder] = others
iex> remainder
[
  %{age: "41", gender: "Male", name: "Naresh"},
  %{age: "45", gender: "Male", name: "Jagdish"}
]
```

What if you still try to get remaining people from `remainder`? It will fail because we are expecting at least 3 people from the right hand side. So, be very careful when you are playing with lists.

```elixir
iex> [first, second, third | some_more] = remainder
** (MatchError) no match of right hand side value: [%{age: "41", gender: "Male", name: "Naresh"}, %{age: "45", gender: "Male", name: "Jagdish"}]
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:25: (file)
```

One of the real use cases where you can use above pattern is to find _min_ number from the list.

```elixir
iex> [min | _] = Enum.sort([3,2,1])
[1, 2, 3]
iex> min
1
```

## Maps

In Lists, we saw how persons are grouped together. But each person is defined using **map** data structure.

```elixir
iex> person1 = %{age: "30", gender: "Male", name: "Amit"}
%{age: "30", gender: "Male", name: "Amit"}
```

As we know, whenever there is a varaible on the left side, it will always match the right side and the right term will be bound to the variable. To fetch any value from a map, we can use `.` (dot) notation as we have it in OOPs world.

```elixir
iex> person1.name
"Amit"
```

But what if I want all 3 values? On the left side, you can have map with variables. The terms on the right side will be bound to respective variables.

```elixir
iex> %{age: age, gender: gender, name: name} = person1;
%{age: "30", gender: "Male", name: "Amit"}
iex> age
"30"
iex> gender
"Male"
iex> name
"Amit"
iex>
```

To simplify, this is same as above.

```elixir
iex> %{age: age, gender: gender, name: name} = %{age: "30", gender: "Male", name: "Amit"}
```

Now, what if I tell you to get the name of first person in the list? You might perform below steps:

**Step 1**:

```elixir
iex> [ person1 | others ] = people
```

**Step 2**:

```elixir
iex> [%{name: name} | others] = person1
```

But for your surprise, you can combine both steps into one.

```elixir
iex> [%{name: name} = person1 | others] = people
iex> name
"Amit"
iex> person1
%{age: "30", gender: "Male", name: "Amit"}
iex>
```

If there is one thing in this article which I really want you to focus on is, it is above pattern. This pattern can be seen almost everywhere in the code. Especially when you receive params from HTTP request in your controllers.

::bonus{noOfStars = 1}
You can also write

`[%{name: name} = person1 | others] = people`

as

`[person1 = %{name: name} | others] = people`
::

But there is still one thing which you might have ignored. If you observe carefully, in maps, the left side can contain less elements than the right side, unlike tuples or lists. This is possible due to **partial-matching** rule. The reason for having this feature in maps is that maps are frequently used to represent **structured** data. In such cases, you are more interested in specifc fields. But yes, if you pass some unknown key on the left side, it will fail.

```elixir
iex> %{name: name, phone: phone} = person1
** (MatchError) no match of right hand side value: %{age: "30", gender: "Male", name: "Amit"}
    (stdlib 4.1.1) erl_eval.erl:496: :erl_eval.expr/6
    iex:15: (file)
```

## Case

We can extend pattern matching in control structure like **case** as well.

```elixir
iex> case {:ok, "Happy Ending"} do
 {:ok, result} -> result
 {:error} -> "Ha ha ha"
 _ -> "I  am always there â€¦"
end
"Happy Ending"
```

## In conclusion

We haven't covered pattern matching in functions as that itself deserves separate article. Always remember, `=` is not an assignment operator. Then what is it? :)
