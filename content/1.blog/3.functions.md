---
title: Functions
description: Function clauses, Function Overloading
createdAt: 2023-01-27
head.title: Functions
head.description: Function clauses, Function Overloading
---

::post-image{imageSrc="header_3.jpg"}
Functions
::

Due to Elixir being functional programming language, you will find any Elixir code inundated with lots of small functions. And that's why functions are first-class citizens in Elixir. This means, you can pass around functions like any normal variables. You might be already accustomed to it if you have used JavaScript before. But there are few nuances in Elixir functions which you will appreciate for sure.

## Modules

Everything starts with Modules, so let's look at that first. A module is a bunch of functions, somewhat similar to a **namespace**. They are grouped together because they are somehow related. In objected-oriented languages, **data** and **functions** are encapuslated in objects. But in Elixir they are kept separate because data in Elixir is immutable and it doesn't make much sense to keep them together. For example, all string manipulation functions are grouped in `String` module and all functions relating to lists are kept in the `List` module.

You can create a module using `defmodule` construct with the contents of the module enclosed between `do` and `end`.

```elixir
defmodule Animals.Feline.Cat do

end
```

By the way they can also be nested. Both above and below are equivalent.

```elixir
defmodule Animals.Feline do
    defmodule Cat do

    end
end
```

It is important to know that a module must be defined in a single file. You cannot have multiple source code files of a single function. But, you can have multiple modules in a single file, which is obvious. LOL.

::bonus{noOfStars = 5}

Like any programming language, Elixir modules must follow some naming conventions. A module starts with an uppercase letter and is usually written in CamelCase style. It can consist of **alphanumerics**, **underscores**, and the **dot (.)** character.

The **dot (.)** character is for your convenience. When the code is compiled, there is no hierarchy. It is just for you to scope your names.

Below code can be referenced as `Animals.Feline.Cat`. Remember, though the code looks hierarchical to you, it is not when compiled.

```elixir
defmodule Animals.Feline do
    defmodule Cat do

    end
end
```

What I like most about modules in Elixir is that you can refer to other modules even before writing them without any compilation errors.

```elixir
defmodule Animals.Feline do
    defmodule Cat do
        IO.puts(Animals.Canine.Dog);
    end
end
```

Here, though the `Animals.Canine.Dog` is not declared anywhere, you can still use it because module names are converted into atoms internally (in the Erlang VM) and have **Elixir** added to the front. So, the module `Animals.Feline` will convert to `:"Elixir.Animals.Feline"` atom internally and that's why compiler doesn't complain as the atom exists. FYI, atoms need to have quotes around them when they contain a special character like a space or dot.

But if you try to call function before it's declared, you will get below error.

```elixir
iex> IO.puts(Animals.Canine.Dog.bark)
** (UndefinedFunctionError) function Animals.Canine.Dog.bark/0 is undefined (module Animals.Canine.Dog is not available)
    Animals.Canine.Dog.bark()
    iex:4: (file)
```

Erlang module names are also atoms, thus Elixir has adopted same approach. If you monitor the modules in Erlang VM, you will be able to distinguish which modules are from Elixir because of the prefix.

::

## Named Functions

Any function having a name associated to it, is called? You guessed it right. Named functions are defined with the `def` keyword followed by a parameter list. Like modules, function body begins with the `do` keyword and ends with the `end` keyword.

```elixir
defmodule OurMathModule do
    def sum(num1, num2) do
        num1 + num2
    end
end
```

It's pretty simple, but one thing to notice is the missing **return** keyword. In Elixir, the value of last statement (or expression) is automatically returned. This is how you write less code. I don't know why other languages haven't adopted this style yet as anyways we can't write anything after **return** keyword.

But having a name to a function the only reason why it's called **named functions**? Actually, named functions can exist only inside a module. The main reason for this is that you can call named functions from **other** modules, unlike _anonymous functions_.

::bonus{noOfStars=3}
I have used the word _keyword_ for **defmodule** and **def** till now. This was just to explain you well. But in reality, these are **macros** and not _keywords_. Macros inject some code dynamically. It's good to understand what is macro in detail, but not required at the moment for you to proceed.
::

### Single-line Functions

Sometimes, we don't need functions processing complex logic. In such cases you can opt for concise syntax.

```elixir
def multiply(num1, num2, num3), do: num1 * num2 * num3
```

Once you start coding, I think you will start appreciating above style as your functions would be composed of many small functions. You will experience it.

### Function Names

Now having just a name is not enough. Whenever you read any official docs (or some good code), you will see something like `multiply/3` and not just `multiply`. The `/3` indicates the no. of parameters the function accepts. So the `3` digit in Elixir is called **arity**. Add this to your vocabulary! :closed_book:

It is **arity** which distinguishes one function from another. Yes, because you can have multiple functions with same name. You might be thinking the right word, yes it is **function overloading**. But ideally, they are completely different functions for Elixir due to _arity_.

```elixir
# multiply/3
def multiply(num1, num2, num3), do: num1 * num2 * num3

# multiply/2
def multiply(num1, num2), do: num1 * num2
```

But what if you have same name with same arity?

```elixir
defmodule OurMathModule do
    def multiply(num1, num2, num3) do
        num1 * num2 * num3
    end

    def multiply(num1, num2, num3) do
        num1 * num2 * num3
    end
end
```

I am sure you must be expecting an exception, but Elixir will load it with a warning indicating that the second function will never be called as only the first instance will be defined. :imp:

## Anonymous Functions

Though I don't need to define what this means because this is everywhere nowadays. It is commonly referred as **lambdas**. You just define a function and assign it to a variable and then play :soccer: around with it. Here, anonymous functions start with `fn` keyword and end with `end`. The body and parameters are separated with `->`. It takes some practice avoiding `=>` which you are so used to. :smiley:

```elixir
iex> multiply = fn (num1, num2) -> num1 * num2 end
#Function<41.3316493/2 in :erl_eval.expr/6>
```

To call the above function, use `.` (dot) operator.

```elixir
iex> multiply.(1,2)
2
```

By the way you can skip paranthesis if you want.

```elixir
iex> multiply = fn num1, num2 -> num1 * num2 end
#Function<41.3316493/2 in :erl_eval.expr/6>
```

Personally, I encourage you to use paranthesis to improve readability. If we have named functions, then why do we even need such anonymous functions? If you just want to use it and throw away, like paper dish, go with anonymous functions. But if you find yourself writing same lambda again and again, it's time to convert it to a named function.

### Capture Operator

Well, anonymous functions help us to shorten the code, you can still shorten it further using **capture operator**. We can rewrite our _multiply function_ as below using capture operator.

```elixir
iex> multiply = &(&1 * &2 * &3)
#Function<40.3316493/3 in :erl_eval.expr/6>
```

It's just that you enclose your function with `&()` and just focus on parameters. What if you have just two parameters with `*` operator?

```elixir
iex> multiply = &(&1 * &2)
&:erlang.*/2
```

Focus **&:erlang.\*/2** :point_up:. Elixir (or Erlang) didn't create new function for us but just rebound the existing `*` operator function to our variable **multiply**. This is true for all existing functions in Erlang. Isn't it smart? It saves memory.

But can you skip the number?

```elixir
iex> multiply = &(&1 * &3)
** (CompileError) iex:3: capture argument &3 cannot be defined without &2 (you cannot skip arguments, all arguments must be numbered)
```

So ensure you follow the sequence of numbers.

It's not that you always need to use `&()` paranthesis. You can use other delimeters as well. But the resuls would be based on the delimeter. For example, if you need to create list, you can create your own funciton like below.

```elixir
iex> create_list = &[&1, &2, &3]
#Function<40.3316493/3 in :erl_eval.expr/6>

iex> create_list.(1, 2, 3)
[1, 2, 3]
```

You might think what value the capture operator is adding apart from reducing the syntax characters. The main idea about this operator is that if you recall, unlike anonymous functions, you cannot pass named functions as parameters to other functions. To make that happen, you can capture complete named function into a variable.

```elixir
iex> multiplyFunc = &OurMatModule.multiply/2
&OurMatModule.multiply/2

iex> multiplyFunc.(1, 2)
2
```

## Default Parameter Values

If you are an experienced developer, you might have noticed little redundancy in above code snippets. You can replace below code

```elixir
defmodule OurMathModule do
    def multiply(num) do
       num * num
    end

    def multiply(num1, num2) do
        num1 * num2
    end
end
```

with

```elixir
defmodule OurMathModule do
    def multiply(num) do
       multiply(num, num)
    end

    def multiply(num1, num2) do
        num1 * num2
    end
end
```

It seems this is obvious. Yes, it is. But often writing more functions in the code we do forget that we can compose functions with other functions. Why did I bring this topic? Because Elixir does this internally when you use **default values**.

To provide a default value to a function, you can use `\\` syntax.

```elixir
def multiply(num1, num2 \\ 5) do
    num1 * num2
end
```

If you call `multiply(5)`, you will get `25`. If you do pass second parameter, it will pick up that value. Again this is common in every language. But there is one thing where Elixir shines.

Let's have one more parameter `num3`.

```elixir
def multiply(num1, num2 \\ 5, num3) do
    num1 * num2 * num3
end
```

What would happen if you call the function with only two arguments? Which parameter will receive the second argument? `num2` or `num3`?

```elixir
iex> multiply(3, 4)
60
```

::bonus{noOfStars = 5}
Elixir will **pick** the _mandatory_ parameters first from the argument list. So, in our case, argument `4` will be given to `num3` as `num2` already has a default value. This is possible because Elixir generates two defintions - `multiply/2` and `multiply/3` - automatically. And when you call `multiply/2`, it calls `multiply/3` with default value.

Unlike other languages, you can have default values at any position in Elixir.
::

## Private Functions

As named functions need to be defined inside a module, by default all functions are **public** and they can be called from other modules. Sometimes, we might need to restrict that. And for this we have **private** functions which you can define using `defp` keyword (macros :smiley:).

```elixir
defmodule OurMathModule do
    def square(num) do
       multiply(num, num)
    end

    defp multiply(num1, num2) do
        num1 * num2
    end
end
```

As per my experience so far, it is bettter to use private functions as much as possible and only expose them as needed. Why? It will make you a good programmer as you will start thinking about **least access** principle.

## Naming Conventions

It is very important that you understand how to name your modules and functions. Elixir doesn't throw an error if you step out of guidelines. But other developers might find it difficult to read your code and understand the intention of the code.

- Module names should use `CamelCase`.

```elixir
# not preferred
defmodule Ourmathmodule do
  ...
end

defmodule Our_Math_Module do
  ...
end

defmodule OurMathModulePi do
  ...
end

# preferred
defmodule OurMathModule do
  ...
end

defmodule OurMathModulePI do
  ...
end
```

- Similar to variables, functions should use `snake_case`.

```elixir
# not preferred
def someFunction do
  ...
end

# preferred
def some_function do
  ...
end
```

- When your function returns a boolean value, it should end with `?`.

```elixir
def even?(num) do
    rem(num, 2) == 0
end
```

But there is a small exception to this. When you use **guards** (functions used as conditionals inside function; keep reading further), you shoud use:

```elixir
defmodule OurMathModule do
  defguard is_even(num) when is_integer(num) and rem(num, 2) == 0

  def my_function(number) when is_even(number) do
  # do stuff
  end
end
```

In short, when you use `is_even`, other developers will know that this is a guard and not a function.

- Use `!` at the end of the name if the function is going to raise an exception (In Elixir, we read it as "functions raise" and not "functions raise exceptions"). You will find this pattern more in core and third-party library functions. This is because not every function will execute successfully if you provide incorrect input.

Below code tries to read the file. If `somefile.txt` exists, it will match `{:ok, body}` clause. If it doesn't, it will match `{:error, reason}`. Here, we are trying to say that if the file doesn't exist, it's kind of okayy. :smiley:

```elixir
iex> case File.read("somefile.txt") do
...>   {:ok, body} -> IO.puts("Success: #{body}")
...>   {:error, reason} -> IO.puts("Error: #{reason}")
...> end
```

But there are times when you really need the file and don't want to proceed with your code if it doesn't exist.

```elixir
iex> File.read!("nosuchfile.txt")
** (File.Error) could not read file "nosuchfile.txt": no such file or directory
    (elixir 1.14.2) lib/file.ex:358: File.read!/1
    iex:5: (file)
```

Now the question is whether you should create two definitions for every function? It really depends on your use case. If you are creating some library then I recommend you do create extra functions. But in your business logic, I think it is fine if you just have one function and return tuples.

::bonus{noOfStars=2}
When there is `size` at the end of the function name, it indicates that operation runs in constant time (O(1)). It means it takes same amount of time to read an element at any position in the list.

When there is `length` at the end, the operation runs in linear time. It means, to read the elements at the end of the list would take more time.
::

## Other Module Functions

Calling a function from another module is very simple. Just prefix the whole module name and then the function.

```elixir
iex> String.length("basics with Amit")
16
```

Here, `String` is a module. If you just want to use function name without module, you need to import the module. But when you do that, you import **all** the functions from it. So be careful as the function name from different modules might be same. For example, if you import `String` module and use `length("basics with Amit")`, it will conflict with `length` function from List (`Kernel` module).

```elixir
iex> length("basics with Amit")
** (CompileError) iex:3: function length/1 imported from both String and Kernel, call is ambiguous
```

All functions (`is_atom`, `div`, etc) available in `Kernel` module can be used without module name as they are automatically imported by Elixir.

Do not confuse **importing** a function with **loading** a function, like Python. Importing here just means, that you can use a function without a module name because the imported function is brought into the current namespace from where you are calling.

One more intersting thing to note here is that functions prefixed with `_` are not imported automatically because such functions are meant not to be used, similar to variables. So, if you still want to use it, you need to import it by name.

### Importing Specific Functions from a Module

You might find it suitable to import all functions from a module. But doing so involves risk of conflicts if there are same name functions in modules which you have imported. So, it is recommended to use module names when possible as it improves code readability. But if you still do not want to use module names every time, you can import **only** functions which you really want.

```elixir
iex> import String, only: [upcase: 1, trim: 1]
```

Here, `upcase: 1` means `upcase/1`, so `:1` indicates arity. If you try to use any other function except **upcase** and **trim**, you will get an error. For rest of the functions from **String** module, you need to use module name.

There is other variant also you can try, `except`.

```elixir
iex> import String, except: [upcase: 1, trim: 1]
```

I am sure you know what :point_up: means.

### Other Ways of Importing

As said, it is good to use function with module names. But sometimes, the module names are so long due to deep hierarchy, you fallback to importing a module. But there is `alias` in Elixir which can help you to rename module name to your choice.

```elixir
iex> alias SomeModule1.AnotherModule2.StillAnotherModule.OurMathModule, as: MathModule
MathModule.multiply(2,2)
4
```

We have just referenced **OurMathModule** with a different name **MathModule**. Now we can use all functions with just **MathModule** reference. This means, we are not importing any function in current namespace, it's just referencing. If you do NOT want to rename your original module name but also do not want to use the long hierarchy, you can go with below option.

```elixir
alias SomeModule1.AnotherModule2.StillAnotherModule.OurMathModule
OurMathModule.multiply(2,2)
4
```

You can skip `:as` option in this case. It is good to know that both `import` and `alias` are lexically scoped. So, you are not forced to write it at the top of the file. You can use them inside functions directly.

```elixir
defModule ExampleModule do

    def to_uppercase(some_string) do
        import String, only: [upcase: 1]
        upcase(some_string)
    end

      def to_lowercase(some_string) do
        alias String, as MyString
        MyString.downcase(some_string)
    end

end
```

We saw the term called **macro** in the beginning. The code which generates code at compile time. If you have macros, which look similar to functions, in your module, you cannot `import` or `alias` them as you do with any public function. You need to expicitly tell that you really **require** that macro.

```elixir
iex> Integer.is_odd(3)
** (UndefinedFunctionError) function Integer.is_odd/1 is undefined or private. However, there is a macro with the same name and arity. Be sure to require Integer if you intend to invoke this macro
    (elixir) Integer.is_odd(3)
iex> require Integer
Integer
iex> Integer.is_odd(3)
true
```

The above example is from Elixir docs. You can read the error. The main question is why `is_odd` is not a simple function? Because it can be used as a **guard**. FYI, `require` is also lexically scoped.

The last way to use any function is `use`. :smiley: I have mostly seen the usage of `use` while writing testcases, creating controllers, etc.

```elixir
defmodule HelloWeb.PageController do
  use HelloWeb, :controller

  def index(conn, _params) do
    render(conn, "index.html")
  end
end
```

Here, you are allowing `HelloWeb` module to inject some of its own code, or even some other modules, defining new functions, etc. Behind the scenes, it uses `require`. Due to its nature of injecting code, I suggest you to use it sparingly and rely on `import` and `alias` as much as possible. Better to read module specific documentation if you really need to use it.

## Module Attributes

When there is a need to store some meta data, you can make use of **module attributes**. They are annotated with `@` at the begining.

```elixir
defmodule OurMathModule do

    @pi 3.14

    def circle_area(radius) do
       @pi * radius ** 2
    end
end
```

In some way, they act like constants, but in reality they are not. Because module attributes can be rebound with new values and constants (in other languages you can't). They are helpful if you want to reuse them at multiple places. Now, you might think why not declare **variables**. Well, you can, but it will occupy memory at runtime. No matter how small value it holds. Whereas, module attributes exists only at **compile time**. It means wherever module attributes are used, they will be replaced with their values. In case you assign a function to the attribute, it will be called at compile time and its return value will be used in place of attributes. Be careful, when you do this, because your function should not do something (like getting data from database) which cannot be resolved at compile time.

```elixir
defmodule OurMathModule do

    @pi 3.14

    def circle_area(radius) do
        @pi 3.14159
        @pi * radius ** 2
    end

    def circle_cirfumference(radius) do
        2 * radius * @pi
    end
end
```

As said, you can rebound new values when needed.

::bonus{noOfStars=5}
If you read same constant (module attribute) in multiple functions, in our case reading `@pi` in both `circle_area/1` and `circle_circumference/1`, Elixir takes snapshot of its current value and ends up creating multiple copies of it. There is no harm in it, but if you have some function call assigned to the constant and if that function takes more time to compute, the compilation time will increase. To avoid this, you can create function and return constant from there.

```elixir
defmodule OurMathModule do

    @pi 3.14

    def circle_area(radius) do
        pi 3.14159
        pi * radius ** 2
    end

    def circle_cirfumference(radius) do
        2 * radius * pi
    end

    def pi, do: @pi
end
```

Creating functions will not only help you here but also if you want to access the constant outside the module. By default, you cannot access constants defined in one module in another module. The only way to get it is from public function, like `pi/0`.

But, if you have more constants and you want to use them in other modules as well, it is quite cumbersome to create functions just for sake of accessing them in other modules. To solve this, you can create your own **macro**.

```elixir
defmodule Constants do
  defmacro const(const_name, const_value) do
    quote do
      def unquote(const_name)(), do: unquote(const_value)
    end
  end
end
```

Now, create some common module for constants.

```elixir
defmodule OurConstants do
    import Constants

    const :pi, 3.14
end
```

And import `OurConstants` module.

```elixir
defmodule OurMathModule do
    import OurConstants

    def circle_area(radius) do
        pi * radius ** 2
    end

    def circle_cirfumference(radius) do
        2 * radius * pi
    end
end
```

One question you may have is why did we create a separate module? Because, as per the official docs, _functions defined in the same module as the attribute itself cannot be called because they have not yet been compiled when the attribute is being defined._

::

## Pattern Matching

Till now we have seen how we can define a function and how to use it. But if we go little deeper then there is lot you can achieve using **pattern matching**. If you don't know what pattern matching in Elixir is, I higly recommend you to go and read below article first and then come back to this section.

::related-posts
[- Patterns, Patterns and Patterns](pattern-matching)
::

To recap, below is an example of Map from above article.

```elixir
iex> %{age: age, gender: gender, name: name} = %{age: "30", gender: "Male", name: "Amit"}
```

The pattern matching consists of two parts: **pattern** (left side) and the **term** (right side). In a match expression, the attempt to match the _term_ to the _pattern_ takes place. If the match succeeds, all variables in the _pattern_ are bound to corresponding values from the _term_. If it doesn't, it will raise an error.

Let's see how we can use Pattern Matching in functions. Below example shows the format we saw earlier.

```elixir
defmodule OurMathModule do
    def multiply(num1, num2) do
        num1 * num2
    end
end
```

We can redefine this same function to accept tuple (or you can have list, etc).

```elixir
defmodule OurMathModule do
    def multiply({num1, num2}) do
        num1 * num2
    end
end
```

To call a function, now you need to pass argument as a tuple with two elements.

```elixir
iex> OurMathModule.multiply({2, 5})
10
```

The tuple `{2, 5}` is matched against the pattern specified in the definition. So, when the function receives this tuple, it will bind its values to **a** and **b** variables.

```elixir
iex> {a, b} = {2, 5}
```

If you provide anything that isn't a two-element tuple, then it will raise an error.

### Multiclause Functions

Moving ahead let's say you decide to write functions to calcuate area of _square_ and _rectangle_. We already saw one for _circle_ before.

```elixir
defmodule OurMathModule do

    @pi 3.14

    def rectangle_area(a, b) do
        a * b
    end

    def square_area(a) do
        a * a
    end

    def circle_area(radius) do
       @pi * radius ** 2
    end
end
```

You must have noticed that all functions are trying to do one thing and that is, calculate **area**. So, rather than having different names for each function (there is nothing wrong in it though), we can take advantage of **function overloading** and **pattern matching**. When you have same function name with same arity, you get function overloading. Basically, you are overloading one function with _different_ responsibilities. :elephant: :elephant:

```elixir
defmodule OurMathModule do

    @pi 3.14

    def area({:rectangle, a, b}) do
        a * b
    end

    def area({:square, a}) do
        a * a
    end

    def area({:circle, radius}) do
       @pi * radius ** 2
    end
end
```

Now, tell me, have we defined **three** functions or **one**? :innocent: Actually it's **one** function with **three** different clauses. A _clause_ is a function definition specified by the `def` construct. If you provide multiple definitions of the same function with the same arity, then you have multiple clauses of the function.

Based on the type of tuple you pass, respective clause will be called.

```elixir
iex> OurMathModule.area({:rectangle, 5, 10})
50
```

When the above code executes, Elixir will match in the order the clauses are defined. The one that matches will be called, else it will raise an error. Now, if you want your own custom error, you can define one more clause to handle that.

```elixir
defmodule OurMathModule do

    @pi 3.14

   # other code

    def area(unknown) do
       {:error, {:unknown_shape, unknown}}
    end
end
```

The above clause will be called if none of the three clauses match. It is because, if you remember, single variable **always** matches the right side term. If you have such clause, ensure you keep it in the last position else every call will match this clause.

One thing to take care of is that the above clause will only work for `area/1` because it expects only one parameter, in our case it is tuple. If you pass multiple arguments, above clause won't execute.

### Guards

From the name itself, you can imagine that the **guards** are to protect something.

```elixir
defmodule OurMathModule do

    # other code

    def area({:rectangle, a, b}) do
        a * b
    end

end
```

Execute above code with string arguments.

```elixir
iex>  OurMathModule.area({:rectangle, "10", "20"})
** (ArithmeticError) bad argument in arithmetic expression: "10" * "20"
    :erlang.*("10", "20")
    iex:14: OurMathModule.area/1
    iex:10: (file)
```

As you can see, just pattern matching alone won't work because variables `a` and `b` can accept anything. Here, we can use guard to protect our function and check whether the arguments passed are numbers and if yes then only execute our function.

```elixir
defmodule OurMathModule do

    # other code

    def area({:rectangle, a, b}) when is_number(a) and is_number(b) do
        a * b
    end

    # skipped area(unknown) clause purposefully

end
```

Trying now will give below error.

```elixir
iex> OurMathModule.area({:rectangle, "10", "20"})
** (FunctionClauseError) no function clause matching in OurMathModule.area/1

    The following arguments were given to OurMathModule.area/1:

        # 1
        {:rectangle, "10", "20"}

    iex:15: OurMathModule.area/1
    iex:12: (file)
```

You can prevent above error using default clause `area(unknown)`.

```elixir
defmodule OurMathModule do

    # other code

    def area({:rectangle, a, b}) when is_number(a) and is_number(b) do
        a * b
    end

    def area(unknown) do
       {:error, {:unknown_shape, unknown}}
    end

end
```

Now you will get your custom error.

```elixir
iex> OurMathModule.area({:rectangle, "10", "20"})
{:error, {:unknown_shape, {:rectangle, "10", "20"}}}
```

You should always use guards if you are defining public functions for other developers to consume. Now you might think, why not do such check inside the function? Well, you can, but because your function **matched**, Elixir won't go and check other functions and it won't reach the default clause written at the end. You have to manage it internally. There are many more intersting things about guards, but we will cover them in a separate article.

By the way, you can combine multiclause and guards with **lambdas** as well.

```elixir
iex> rectangle_area =
        fn
            (a, b) when is_number(a) and is_number(b) -> a * b
        end

iex> area.(2,3)
6

iex> rectangle_area.(2, "3")
** (FunctionClauseError) no function clause matching in :erl_eval."-inside-an-interpreted-fun-"/2

    The following arguments were given to :erl_eval."-inside-an-interpreted-fun-"/2:

        # 1
        2

        # 2
        "3"

    (stdlib 4.1.1) :erl_eval."-inside-an-interpreted-fun-"/2
    (stdlib 4.1.1) erl_eval.erl:898: :erl_eval.eval_fun/8
    iex:2: (file)
```

Because we don't have any default clause Elixir threw above error. Let's add one.

```elixir
iex> rectangle_area =
    fn
        (a, b) when is_number(a) and is_number(b) -> a * b
        (_, _) -> {:error, :invalid_arguments}
    end

iex> rectangle_area.(2, "3")
{:error, :invalid_arguments}
```

## In conclusion

If you have reached here, it means you are desperate about learning Elixir. Be motivated and build scalable systems. :clap: :clap: :clap:
