---
title: The Mysterious @conn in Phoenix
description: Let's see how a @conn is processed by Phoenix and the response you see eventually on your browser.
createdAt: 2022-12-01
head.title: The Mysterious @conn in Phoenix
head.description: Let's see how a @conn is processed by Phoenix and the response you see eventually on your browser.
---

# The Mysterious @conn in Phoenix

When I started learning **Elixir** long back, I used to wonder why this **@conn** struct (there are no objects in Elixir world) is seen everywhere - _plugs_, _routes_, _controllers_, _views_ and in _templates_. In this post we will see how the **_conn_** is transformed along the way. Yes, in each step, a new **_conn_** struct is created.

To be a better Phoenix developer, this post is a must read. It's long but worth your time.

## Two Worlds

We know Elixir relies on Erlang heavily. In fact, when you create an Elixir app, it is Erlang OTP (Open Telecom Platform) app you are creating. Under the hood, Elixir talks to Erlang to process all your requests. Let's see some of the unfamiliar members of both worlds.

![](/images/1_1.png){m-auto}

### Erlang World

People living in this world speak Erlang language with their strength called _BEAM_. For starters, _BEAM_ is a virtual machine that executes user code in the Erlang Runtime System (ERTS).

**Cowboy**

It is _the_ default HTTP server for Erlang/OTP. It is similar to _Apache_ or _Nginx_. The difference is that unlike Apache and Nginx, which serve requests by mapping each HTTP request to file on the disk, _Cowboy_ maps each HTTP request to an Erlang module. It works in tandem with _Ranch_ which is a socket worker pool for TCP.

```json{filename="assa.md"}
// A simple Nginx configuration

server {
	 listen 80;
	 root /var/www/basicswithamit.com/public_html;
	 server_name basicswithamit.com;
}
```

```xml
<!-- A simple Apache configuration -->

<VirtualHost *:80>
  DocumentRoot "/var/www/basicswithamit.com/public_html"
  ServerName basicswithamit.com
</VirtualHost>
```

_Cowboy_ has no idea of the files on the disk. In below example, you can see, it maps every HTTP request to _MyApp.Cowbboy.Handler_ module.

```elixir
dispatch_config = :cowboy_router.compile([
    { :_,
		[
				{:_, MyApp.Cowboy.Handler, []},
		]}
])

{ :ok, _ } = :cowboy.start_http(:http,
	100,
	[{:port, 8080}],
	[{ :env, [{:dispatch, dispatch_config}]}]
	)
```

**Ranch**

Cowboy understands how to handle the HTTP protocol but it still does not know how to handle incoming socket connections and managing the TCP protocol, and for this it depends on _Ranch_ to do the work. _Ranch_ is a socket acceptor pool toolkit for TCP protocols written in Erlang. It has two main parts:

_Ranch Acceptors_: A group of processes that are actively accepting connections from external clients.

_Ranch Connections_: Once the acceptor accepts a new connection, it delegates to this process to actually begin processing the incoming requests.

### Elixir World

We, modern people who find Erlang complex to understand, just speak Elixir. This language talks to Erlang to carry out its day-to-day job.

**Plug**

It is a web server interface for Elixir. It is similar to _Rack_ in the Ruby world. Plug gives us a few ready-made implementations: _CSRF protection_, _sessions_, _logging_ or serving _static files_. There are various plugs that come by default when you create a new phoenix project. Some of them are:

- MyPhoenixApp.Endpoint: The entry point of your application. Only _Conn_ can come in and go out.
- MyPhoenixApp.Router
- MyPhoenixApp.Controller
- MyPhoenixApp.View

It is most likely that you will also write a custom plug to authenticate requests.

**Phoenix**

Before we understand the **@conn** in detail, let's create a Phoenix app.

When you run below command, Phoenix creates an Elixir OTP app which follows the same rules that apply to any normal OTP app.

```elixir
mix phx.new my_phoenix_app
```

When you give below command, you are creating a world of Phoenix that is ready to accept requests and give out responses.

```elixir
mix phx.server
```

Now, check the _application_ function inside your _mix.exs_ file. It returns a keyword list with keys **mod** and **applications**. All applications defined against _applications_ key are executed in the sequence they are mentioned in.

```elixir
# mix.exs

def application do
  [mod: {MyPhoenixApp, []},
   applications: [:phoenix, :phoenix_pubsub, ...]]
end
```

It is the _MyPhoenixApp_ module which is reffered when you run your application through **iex -S mix** or **mix phx.server** command. Below _start_ function, inside _MyPhoenixApp_ module is automatically defined when you create the app and invoked when the app is started.

```elixir
# in lib/my_phoenix_app.ex

def start(_type, _args) do
	import Supervisor.Spec

	children = [
		supervisor(MyPhoenixApp.Repo, []),
		supervisor(MyPhoenixApp.Endpoint, []),
	]
	opts = [strategy: :one_for_one, name: MyPhoenixApp.Supervisor]
	Supervisor.start_link(children, opts)
end
```

In the above code, we can see that _Supervisor.start_link_ code start two more supervisors (processes), each for **Repo** and **Endpoint**. Supervisor's main job is to monitor its sub-processes. If any of the process stops working, it will be replaced based on the strategy defined. Here it is **one_for_one**. It means, one dead process will be replaced with brand new process. There are multiple such strategies which you can define based on requirements.

We won't dig into _Repo_ as our aim is to learn more about _conn_, so let's chek what is there inside _Endpoint_

```elixir
# in lib/my_phoenix_app/endpoint.ex

defmodule MyPhoenixApp.Endpoint do
  use Phoenix.Endpoint, otp_app: :my_phoenix_app
	plug Plug.RequestId
  	plug Plug.Logger
	...
  	plug MyPhoenixApp.Router
end
```

_MyPhoenixApp.Endpoint_ is a child supervisor of your _MyPhoenixApp_ supervisor. If you notice carefully, you won't see **_start_link_** function in this supervisor. It is because, you have told `use Phoenix.Endpoint, otp_app: :my_phoenix_app` macro to inject that function dynamically with all the sub-processes (workers) details that it needs to monitor. And one of the automatically defined workers for your phoenix app is an embedded _ranch_ supervisor. This supervisor in turn starts _Cowboy_ process listening at port 4000 on localhost and is configured to send all requests to _Plug.CowboyHandler_.

## The @conn

Now that we have basic understanding of Phoenix, lets start discussion about **conn**. It is nothing but a simple struct with lots of keys to store both request and response unlike other frameworks where both are stored separately.

When you hit `localhost:4000` in your browser, the sequence of action goes as below:

1. `Plug.Adapter.Cowboy.Handler` gets called with the request information.
2. From the request data, the **Handler** creates below **conn** struct.

```elixir
%Plug.Conn{
    adapter: {Plug.Adapters.Cowboy.Conn, req},
    host: host,
    method: meth,
    owner: self(),
    path_info: split_path(path),
    peer: peer,
    port: port,
    remote_ip: remote_ip,
    query_string: qs,
    req_headers: hdrs,
    request_path: path,
    scheme: scheme(transport)
 }
```

## @conn in the Endpoint

1. `Plug.Adapters.Cowboy.Handler` then invokes `MyPhoenixApp.Endpoint.call` passing in the newly created **conn** struct.
2. The module `MyPhoenixApp.Endpoint` got passed as argument when we initially started our Phoenix server using `mix phx.server`.
3. This _conn_ struct will pass through all the plugs defined in _MyPhoenixApp.Endpoint_ and the last plug which will receive this struct would be `plug MyPhoenixApp.Router` and this is the plug where we mention all the routes of our application.

_MyPhoenixApp.Endpoint_ is pretty much the definition of what plugs your application is built of.

```elixir
defmodule MyPhoenixAppWeb.Endpoint do
  use Phoenix.Endpoint, otp_app: :my_phoenix_app

  # The session will be stored in the cookie and signed,
  # this means its contents can be read but not tampered with.
  # Set :encryption_salt if you would also like to encrypt it.
  @session_options [
    store: :cookie,
    key: "_my_phoenix_app_key",
    signing_salt: "80l0urc6"
  ]

  socket "/live", Phoenix.LiveView.Socket, websocket: [connect_info: [session: @session_options]]

  # Serve at "/" the static files from "priv/static" directory.
  #
  # You should set gzip to true if you are running phx.digest
  # when deploying your static files in production.
  plug Plug.Static,
    at: "/",
    from: :my_phoenix_app,
    gzip: false,
    only: ~w(assets fonts images favicon.ico robots.txt)

  # Code reloading can be explicitly enabled under the
  # :code_reloader configuration of your endpoint.
  if code_reloading? do
    socket "/phoenix/live_reload/socket", Phoenix.LiveReloader.Socket
    plug Phoenix.LiveReloader
    plug Phoenix.CodeReloader
    plug Phoenix.Ecto.CheckRepoStatus, otp_app: :my_phoenix_app
  end

  plug Phoenix.LiveDashboard.RequestLogger,
    param_key: "request_logger",
    cookie_key: "request_logger"

  plug Plug.RequestId
  plug Plug.Telemetry, event_prefix: [:phoenix, :endpoint]

  plug Plug.Parsers,
    parsers: [:urlencoded, :multipart, :json],
    pass: ["*/*"],
    json_decoder: Phoenix.json_library()

  plug Plug.MethodOverride
  plug Plug.Head
  plug Plug.Session, @session_options
  plug MyPhoenixAppWeb.Router
end
```

## @conn in the Router

```elixir
defmodule MyPhoenixAppWeb.Router do
  use MyPhoenixAppWeb, :router

  pipeline :browser do
    plug :accepts, ["html"]
    plug :fetch_session
    plug :fetch_live_flash
    plug :put_root_layout, {MyPhoenixAppWeb.LayoutView, :root}
    plug :protect_from_forgery
    plug :put_secure_browser_headers
  end

  pipeline :api do
    plug :accepts, ["json"]
  end

  scope "/", MyPhoenixAppWeb do
    pipe_through :browser

    get "/", PageController, :index
  end

  .....
end
```

As soon as the _conn_ struct is received by Router plug, it performs two things:

1. It finds the matching route based on the request path present in _conn_ and store that function in _conn_ struct.
2. And then, before executing the stored function, it passes on the _conn_ to the pipeline configured. In above code, _conn_ will pass through all plugs defined in the **:browser** pipleline.

The stored function for '/' path looks like below:

```elixir
fn conn ->
  plug = MyPhoenixApp.PageController
  opts = plug.init(:index)
  plug.call(conn, opts)
end)
```

## @conn in the PageController

If you observe the above stored function, router is infact calling `MyPhoenixApp.PageController.init`. The `init` function is defined automatically in **PageController** by `use MyPhoenixApp, :controller` macro. Other automatically injected function is `call`. The main responsibility of this function is to check if there are any other plugs defined at the controller level. If it finds any, it will execute them in sequene passing in the _conn_ struct and at the end call the controller action **:index** which got passed in **opts** struct.

If you open **PageController**, you will see a call to `render conn, "index.html"`. As no view name is mentioned in this call, it is automatically derived from the controller name which in this case is `PageView`. Now, if you check `PageView` file, you won't find any functions defined. But due to metaprogramming (macro) from `use MyPhoenixWeb, :view`, all our template files at `web/templates/page/index.html.eex` get compiled into Elixir functions inside `PageView`, which make them extremely performant. 

In below function, you can use all the properties from _conn_ under _assigns_.

```elixir
defmodule MyPhoenixWeb.PageView do
  use MyPhoenixWeb, :view

  def render("index.html", assigns) do
    "rendering with assigns #{inspect Map.keys(assigns)}"
  end
end
```

## Nested Structure

Once the _conn_ struct reaches the respective view, the value is returned back to the `Plug.Adapter.Cowboy.Handler` which triggered this nested call. At first it might seem the plugs are executed in sequence, but actually they are executed in nested structure. Below is the Endpoint plug nested function.

```elixir
case(Plug.Static.call(conn, {[], {:my_phoenix_app, "priv/static"}, false, false, "public, max-age=31536000", "public", ["css", "fonts", "images", "js", "favicon.ico", "robots.txt"], [], %{}})) do
  %Plug.Conn{halted: true} = conn ->
    nil
    conn
  %Plug.Conn{} = conn ->
    case(Phoenix.LiveReloader.call(conn, [])) do
      %Plug.Conn{halted: true} = conn ->
        nil
        conn
      %Plug.Conn{} = conn ->
        case(Phoenix.CodeReloader.call(conn, reloader: &Phoenix.CodeReloader.reload!/1)) do
          %Plug.Conn{halted: true} = conn ->
            nil
            conn
            .........
            # rest of the code
            ..........
             _ ->
            raise("expected Phoenix.CodeReloader.call/2 to return a Plug.Conn, all plugs must receive a connection (conn) and return a connection")
        end
      _ ->
        raise("expected Phoenix.LiveReloader.call/2 to return a Plug.Conn, all plugs must receive a connection (conn) and return a connection")
    end
  _ ->
    raise("expected Plug.Static.call/2 to return a Plug.Conn, all plugs must receive a connection (conn) and return a connection")
end
```

If the static plug sets the conn's **halted** property to _true_, then it does nothing and returns _nil_. Else, it will call the next plug function with the new _conn_. At the end, the _conn_ struct contains the request and response information having passed through all layers of the app. This final struct is sent back to `Plug.Adapter.Cowboy.Handler` which in turn invokes `:cowboy_re.reply/4` with **http_status**, **headers**, **body**, **request** extracted from the _conn_.

## In conclusion

When you hit url in browser, **Plug.Adapter.Cowboy.Handler** triggers **MyPhoenixApp.Endpoint.call** passing _conn_ struct with all request information. From there, _conn_ passes through all plugs defined in Endpoint, then through Router, Controller and finally View. And then, the processed _conn_ is sent back to **Plug.Adapter.Cowboy.Handler** which then sends the response to Cowboy server by invoking `:cowboy_req.reply/4`.